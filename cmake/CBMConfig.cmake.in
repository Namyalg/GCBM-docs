if (CMAKE_VERSION VERSION_LESS 3.10.0)
    message(FATAL_ERROR "CBM requires at least CMake version 3.10.0")
endif()

if (NOT CBM_FIND_COMPONENTS)
    set(CBM_NOT_FOUND_MESSAGE "The CBM package requires at least one component")
    set(CBM_FOUND False)
    return()
endif()


set(_CBM_FIND_PARTS_REQUIRED)
if (CBM_FIND_REQUIRED)
    set(_CBM_FIND_PARTS_REQUIRED REQUIRED)
endif()
set(_CBM_FIND_PARTS_QUIET)
if (CBM_FIND_QUIETLY)
    set(_CBM_FIND_PARTS_QUIET QUIET)
endif()

get_filename_component(_CBM_install_prefix "${CMAKE_CURRENT_LIST_DIR}" ABSOLUTE)

set(_CBM_NOTFOUND_MESSAGE)

# Let components find each other, but don't overwrite CMAKE_PREFIX_PATH
set(_CBM_CMAKE_PREFIX_PATH_old ${CMAKE_PREFIX_PATH})
set(CMAKE_PREFIX_PATH ${_CBM_install_prefix})

foreach(module ${CBM_FIND_COMPONENTS})
    find_package(${module}
        ${_CBM_FIND_PARTS_QUIET}
        ${_CBM_FIND_PARTS_REQUIRED}
        PATHS "${_CBM_install_prefix}" NO_DEFAULT_PATH
    )
    if (NOT ${module}_FOUND)
        if (CBM_FIND_REQUIRED_${module})
            set(_CBM_NOTFOUND_MESSAGE "${_CBM_NOTFOUND_MESSAGE}Failed to find CBM component \"${module}\" config file at \"${_CBM_install_prefix}/${module}/${module}Config.cmake\"\n")
        elseif(NOT CBM_FIND_QUIETLY)
            message(WARNING "Failed to find CBM component \"${module}\" config file at \"${_CBM_install_prefix}/${module}/${module}Config.cmake\"")
        endif()
    endif()

    # For backward compatibility set the LIBRARIES variable
    list(APPEND CBM_LIBRARIES "CBM::${module}")
endforeach()

# Restore the original CMAKE_PREFIX_PATH value
set(CMAKE_PREFIX_PATH ${_CBM_CMAKE_PREFIX_PATH_old})

if (_CBM_NOTFOUND_MESSAGE)
    set(CBM_NOT_FOUND_MESSAGE "${_CBM_NOTFOUND_MESSAGE}")
    set(CBM_FOUND False)
endif()
